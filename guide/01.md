# Getting started

## Digital ocean
- Make a digital ocean account
- terraform logout (this is just to be safe)
- Make an api key, keep a note of it
- `brew install doctl` and login

## Terraform getting started
- Make a `main.tf` file
- We'll be using the official digital ocean [provider](https://registry.terraform.io/providers/digitalocean/digitalocean/latest/docs)
```terraform
terraform {
  required_providers {
    digitalocean = {
      source  = "digitalocean/digitalocean"
      version = "~> 2.0"
    }
  }
}

# Set the variable value in *.tfvars file
# or using -var="do_token=..." CLI option
variable "do_token" {
  default = "" # I set my token here, insecure but easier
}

# Configure the DigitalOcean Provider
provider "digitalocean" {
  token = var.do_token
}
```
- `terraform init` to get deps
- `terraform plan` to see what changes this will make (it won't make any)
- Add a digital ocean [kubernetes cluster](https://registry.terraform.io/providers/digitalocean/digitalocean/latest/docs/resources/kubernetes_cluster)
```terraform
resource "digitalocean_kubernetes_cluster" "cluster" {
  name   = "harry"
  region = "lon1"
  version = "1.29.1-do.0"

  node_pool {
    name       = "worker-pool"
    size       = "s-1vcpu-2gb"
    node_count = 1
  }
}
```
- `terraform plan` to see what it'll do
- `terraform apply`
- Have a look on the digital ocean website and check it's been created

## Kubernetes getting started
- We've now made a cluster in digital ocean, we now want to connect it to kubernetes
- We'll be using the official kubernetes [provider](https://registry.terraform.io/providers/hashicorp/kubernetes/latest/docs)
```terraform
provider "kubernetes" {
  host = digitalocean_kubernetes_cluster.cluster.endpoint
  token   = digitalocean_kubernetes_cluster.cluster.kube_config[0].token
  cluster_ca_certificate = base64decode(
    digitalocean_kubernetes_cluster.cluster.kube_config[0].cluster_ca_certificate
  )
}
```
- Terraform is now connected to the cluster and can start making changes to it
- Let's make a deployment
```terraform
resource "kubernetes_deployment" "deployment" {
  metadata {
    name = "nginx-example"
    labels = {
      app = "NginxExample"
    }
  }

  spec {
    replicas = 1
    selector {
      match_labels = {
        app = "NginxExample"
      }
    }
    template {
      metadata {
        labels = {
          app = "NginxExample"
        }
      }
      spec {
        container {
          image = "nginx:latest"
          name  = "example"

          port {
            container_port = 80
          }
        }
      }
    }
  }
}
```
- `terraform init`
- `terraform apply`
- It'll now be created, but we can't see it running yet
- Let's connect kubectl to the cluster
- https://docs.digitalocean.com/products/kubernetes/how-to/connect-to-cluster/
- `doctl kubernetes cluster kubeconfig save harry`, this will also change your current cluster
- `kubectl config get-contexts`
- `kubectl get pods` Yay! It's there!
- `kubectl port-forward <pod-name> 3000:80`
- `kubectl describe deployment nginx-example`
- Now expose it to the internet using a [service](https://registry.terraform.io/providers/hashicorp/kubernetes/latest/docs/resources/service)
```terraform
resource "kubernetes_service" "nginx-example" {
  metadata {
    name = "nginx-example"
  }
  spec {
    selector = {
      app = kubernetes_deployment.deployment.metadata[0].labels.app
    }
    port {
      port        = 8080
      target_port = 80
    }
    type = "LoadBalancer"
  }
}
```
- `k get service` will list it's external ip mine was `206.189.246.3`
- go to `http://206.189.246.3:8080/` and see your nginx
- This probably isn't the best way to get it actually exposed but is fine for now
- If you were doing this properly you'd use a load balancer from digital ocean I think