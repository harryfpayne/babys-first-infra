# Deploying a website

## Dockerizing
- We'll make a nextjs app `yarn create next-app frontend`
- Make a docker file that will put this website into a docker image [you can just google this](https://github.com/vercel/next.js/blob/canary/examples/with-docker/Dockerfile)
- We'll build it locally for now `docker build .`

## Setting up a container registry
- We've made the docker image locally but there's no way for the cluster to access it
- We'll put it in a digital ocean container registry
- We need to make the container registry in digital ocean
```terraform
resource "digitalocean_container_registry" "container_registry" {
  name                   = "harry"
  subscription_tier_slug = "basic"
}
```
- We now need to authenticate our local docker to be allowed to push images to it
- `docker login registry.digitalocean.com` email is you email, password is the api token
- In the same way we just had to authenticate our local docker, we need to auth the cluster to read from the container registry
```terraform
resource "digitalocean_container_registry_docker_credentials" "container_registry_credentials" {
  registry_name = digitalocean_container_registry.container_registry.name
}
```
- https://registry.terraform.io/providers/digitalocean/digitalocean/latest/docs/resources/container_registry_docker_credentials
- This makes some connection config
- We then need to make a secret in the cluster to store this
```terraform
resource "kubernetes_secret" "docker_credentials" {
  metadata {
    name = "docker-credentials"
  }

  data = {
    ".dockerconfigjson" = digitalocean_container_registry_docker_credentials.container_registry_credentials.docker_credentials
  }

  type = "kubernetes.io/dockerconfigjson"
}
```

## Using the image
- We're now ready to start using the image we made
- tag the docker image we made `docker build --platform=linux/amd64 -t registry.digitalocean.com/harry/frontend .`
- N.B. You need to build it with `--platform=linux/amd64` because it's running on a linux machine not an m1 mac
- push it up `docker push registry.digitalocean.com/harry/frontend`
- we can then see it on the digital ocean website to check it got pushed up correctly
- https://stackoverflow.com/questions/32726923/pulling-images-from-private-registry-in-kubernetes
```terraform
resource "kubernetes_deployment" "frontend" {
  metadata {
    name = "frontend"
    labels = {
      app = "frontend"
    }
  }

  spec {
    replicas = 1
    selector {
      match_labels = {
        app = "frontend"
      }
    }
    template {
      metadata {
        labels = {
          app = "frontend"
        }
      }
      spec {
        image_pull_secrets {
          name = kubernetes_secret.docker_credentials.metadata[0].name
        }

        container {
          image = "${digitalocean_container_registry.container_registry.endpoint}/frontend"
          name  = "example"

          port {
            container_port = 3000
          }
        }
      }
    }
  }
}
```
- N.B. We could hard code the image name, but I want to reuse as many variables as possible
- Let's also make a new service
```terraform
resource "kubernetes_service" "frontend" {
  metadata {
    name = "frontend"
  }
  spec {
    selector = {
      app = kubernetes_deployment.frontend.metadata[0].labels.app
    }
    port {
      port        = 3000
      target_port = 3000
    }
    type = "LoadBalancer"
  }
}
```
- `terraform apply`
- `kubectl get services`
- Go to its external ip to see it deployed

